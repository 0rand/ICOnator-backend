buildscript {
    dependencies {
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.13.0')
    }
}

// These plugins are used for the subprojects as described here:
// https://stackoverflow.com/questions/26236308/how-to-apply-plugin-to-allprojects-with-new-gradle-plugins-mechanism
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.0.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.4.RELEASE' apply false
    id 'org.unbroken-dome.test-sets' version '1.4.2' apply false
}

// Don't generate a build folder in root
jar.enabled = false

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        // Used for https://github.com/kenglxn/QRGen, they publish on github
        maven { url "https://jitpack.io" }
        maven { url 'https://dl.bintray.com/ethereum/maven/' }
        // Required due to "com.sleepycat" dependency - used by qpid-broker
        maven { url 'http://download.oracle.com/maven' }
        // Used for rates
        maven { url 'https://raw.githubusercontent.com/blockchain/api-v1-client-java/mvn-repo/' }
    }
}

subprojects {

    dependencies {
        testCompile 'junit:junit:4.12'
    }

    ext.dockerVersion = project.hasProperty('dockerVersion') ? project['dockerVersion'] : ''
    ext.dockerImageName = project.hasProperty('dockerImageName') ? project['dockerImageName'] : ''

}

// Don't generate a build folder in the root of the following projects:
configure([project(':commons'), project(':services')]) {
    jar.enabled = false
}

task stage(dependsOn: ['build', 'clean'])
build.mustRunAfter clean

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination "${buildDir}/reports/jacoco/report.xml"
        html.enabled false
        csv.enabled false
    }
}

codeCoverageReport.dependsOn {
    subprojects*.test
}