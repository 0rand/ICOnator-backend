// These plugins are used for the subprojects as described here:
// https://stackoverflow.com/questions/26236308/how-to-apply-plugin-to-allprojects-with-new-gradle-plugins-mechanism
plugins {
    id 'java'
    id 'org.springframework.boot' version '2.0.0.RELEASE' apply false
    id 'io.spring.dependency-management' version '1.0.4.RELEASE' apply false
    id 'org.unbroken-dome.test-sets' version '1.4.2' apply false
}

// Don't generate a build folder in root
jar.enabled = false

subprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
        // Used for https://github.com/kenglxn/QRGen, they publish on github
        maven { url "https://jitpack.io" }
        maven { url 'https://dl.bintray.com/ethereum/maven/' }
        // Required due to "com.sleepycat" dependency
        maven { url 'http://download.oracle.com/maven' }
        // Used for rates
        maven { url 'https://raw.githubusercontent.com/blockchain/api-v1-client-java/mvn-repo/' }
    }

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

// Don't generate a build folder in the services directory
configure([project("services")]) {
    jar.enabled = false
}

// Don't generate a build folder in the commons directory
configure([project("commons")]) {
    jar.enabled = false
}

// Define which tasks Heroku will run before starting the server
task stage(dependsOn: ['services:core:build', 'services:monitor:build'])
